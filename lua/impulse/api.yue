import "http.request" as request

class Notion
  @base_url: "https://api.notion.com"
  @timeout: 30
  @page_size: 100

  new: (api_key) =>
    @api_key = api_key

  _request: (api, method = "GET", body = nil, timeout = @@timeout) =>
    req = request.new_from_uri "#{ @@base_url }/#{ api }"
    with req
      .headers\upsert ":method", method
      .headers\upsert "user-agent", "impulse.nvim-0.1-alpha"
      .headers\append "x-req-by", "impulse.nvim"
      .headers\append "accept", "application/json"
      .headers\append "content-type", "application/json"
      .headers\append "authorization", "Bearer #{ @api_key }"
      .headers\append "notion-version", "2022-02-22"

      if body
        vim.json.encode body
          |> \set_body

    head, resp = req\go timeout
    unless head
      return error "unable to make request to #{ api }"
    
    resp\get_body_as_string!
      |> vim.json.decode

  -- get_page: retrieve a page's properties
  -- this does NOT get content of the page; use `get_blocks` with the page ID.
  get_page: (id) =>
    @\_request "v1/pages/#{ id }"

  -- update_page: update a page's properties
  update_page: (id, data = nil) =>
    @\_request "v1/pages/#{ id }", "PATCH", data

  -- get_block: retrieve a single block
  get_block: (id) =>
    @\_request "v1/blocks/#{ id }"

  -- get_blocks: retrieve all child blocks of a block.
  -- essentially, a page == a block, so you can use this to get a page's content.
  get_blocks: (parent_id, start_cursor = 0, page_size = @@page_size) =>
    qs = "page_size=#{ page_size }"
    qs ..= "&start_cursor=#{ start_cursor }" if start_cursor > 0

    @\_request "v1/blocks/#{ parent_id }/children?#{ qs }"

  -- search: search & filter all pages and databases matching a query
  search: (query = "", filter = nil) =>
    unless filter
      filter =
        property: "object"
        value: "page"

    -- will need to support pagination
    @\_request "v1/search", "POST", { :query, :filter }

export default Notion
